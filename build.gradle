plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.27.0"
    id 'jacoco'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://maven.orangelabs.moe/releases/'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

dependencies {
    compile 'com.google.guava:guava:28.0-jre'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.jetbrains:annotations:17.0.0'
    compile 'moe.orangelabs:json:4.0'

    testCompile 'org.testng:testng:6.14.3'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.assertj:assertj-core:3.12.2'
    testCompile 'org.slf4j:slf4j-simple:1.7.26'
}

test {
    useTestNG()
}

publishing {
    publications {
        maven(MavenPublication) {
            group 'moe.orangelabs'
            artifactId 'jsonrpc'
            version System.getenv("GITHUB_REF") == null ?
                    new Date().getTime() + '-SNAPSHOT' :
                    System.getenv("GITHUB_REF").split("/").last()

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            System.setProperty("org.gradle.s3.endpoint", "https://maven.orangelabs.moe.storage.yandexcloud.net/")
            def releasesRepoUrl = "s3://releases"
            def snapshotsRepoUrl = "s3://snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(AwsCredentials) {
                accessKey System.getProperty("awsCredentials.AWSAccessKeyId")
                secretKey System.getProperty("awsCredentials.AWSSecretKey")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) { it.dependsOn test }

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jacocoTestReport{
    dependsOn test
}
